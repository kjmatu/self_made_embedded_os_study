OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
	/* RAMの定義 */
	ramall(rwx) : o = 0xffbf20, l = 0x004000 /* 16KB */
	ram(rwx) : o = 0xffc020, l = 0x003f00
	stack(rw)   : o = 0xffff00, l = 0x000000 /* end of RAM */
}

SECTIONS
{
	.text : {
		/* テキスト領域 */
		/* textセクションの先頭を指すシンボルを配置 */
		_text_start = . ;
		*(.text)
		/* textセクションの終端を指すシンボルを配置 */
		_etext = . ;
	} > ram /* .textセクションはROMに配置する */

	/* const定義した変数や文字列リテラルが配置される */
	/* read only dataのこと */
	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = . ;
	} > ram

	.data : {
		_data_start = . ;
		/* データ領域 */
		*(.data)
		_edata = . ;
	} > ram /* .dataセクションの配置先をRAMに変更*/

	.bss : {
		_bss_start = . ;
		/* BSS領域 */
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > ram /* .bssセクションの配置先をRAMに変更*/

	/* 4バイトのアライメント */
	. = ALIGN(4);
	_end = . ;

	.stack : {
		/* stackを指すシンボルを配置する */
		_stack = . ;
	} > stack
}